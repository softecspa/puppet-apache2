# = Class: apache2::paramfile
#
# This class manage performance tuning on apache2 webserver. It creates a file under /etc/apache2/conf.d directory containing directive passed as parameter
#
# [*filename*]
#   name of the file created under /etc/apache2/conf.d. If isn.t set <name> will be used
#
# [*order*]
#   Specify an order if the define is used more times on the same file (specifying the same filename)
#
# [*params*]
#   hash of directive to push in form { 'param1' => $value1, 'param2' => $value2 }
#
# [*ifmodule*]
#   put the parameters under <IfModule>. Specify module name
#
# [*directory*]
#   put the parameters under <Directory>. Specify directory path
#
define apache2::paramfile (
  $params,
  $filename ='',
  $order    =1,
  $ifmodule  ='',
  $directory ='',
) {

  if !is_hash($params) {
    fail('params must be in form of hash')
  }

  if $order==0 {
    fail('order parameter cannot be less then 1!')
  }

  $file=$filename? {
    ''      => $name,
    default => $filename,
  }

  if !defined(Concat_build[$file]) {
    concat_build{$file :
      order         => ['*.tmp'],
      target        => "${apache2::apache2confd}/${file}",
    }

    file {"${apache2::apache2confd}/${file}":
      owner   => 'root',
      group   => 'root',
      mode    => '0644',
      notify  => Exec['apache2-reload'],
      require => Concat_build[$file],
    }
  }

  if !defined(Concat_fragment["${file}+0.tmp"]) {
    concat_fragment{"${file}+0.tmp":
      content => '#Generated by puppet'
    }
  }

  concat_fragment {"${file}+$order-${name}.tmp":
    content => template('apache2/etc/tuning_fragment.erb')
  }

}
